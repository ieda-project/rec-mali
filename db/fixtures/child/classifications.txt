$version 50

@signes_danger = danger.boire OR danger.vomit OR danger.convulsions_passe OR danger.lethargie OR danger.convulsions_present

danger|Pas de signe général de danger|low|!@signes_danger
danger|Signe général de danger présent|high|@signes_danger



@pneumonie_grave = toux.toux_presence AND (@signes_danger OR toux.tirage OR toux.stridor)
@frequence_elevee = ((enfant.months >= 12) AND (toux.freq_resp >= 40)) OR ((enfant.months < 12) AND (toux.freq_resp >= 50))

toux|Pas de pneumonie : toux ou rhume|low|toux.toux_presence AND !@frequence_elevee AND !toux.tirage AND !toux.stridor AND !@signes_danger
toux|Pneumonie|medium|toux.toux_presence AND @frequence_elevee AND !toux.tirage AND !toux.stridor AND !@signes_danger
toux|Pneumonie grave ou maladie très grave|high|@pneumonie_grave



@deshydratation_severe = diarrhee.diarrhee_presence AND AT_LEAST_TWO_OF(diarrhee.conscience_agitation == lethargique_inconscient, diarrhee.soif == impossible, diarrhee.yeux_enfonces, diarrhee.pli_cutane == tres_lentement)
@deshydratation_evident = diarrhee.diarrhee_presence AND AT_LEAST_TWO_OF(diarrhee.conscience_agitation == agite_irritable, diarrhee.yeux_enfonces, diarrhee.soif == avide, diarrhee.pli_cutane == lentement) AND !@deshydratation_severe
@pas_de_deshydratation = diarrhee.diarrhee_presence AND !@deshydratation_evident AND !@deshydratation_severe
@diarrhee_persistente = diarrhee.diarrhee_presence AND (diarrhee.diarrhee_presence_duree >= 14) AND @pas_de_deshydratation
@diarrhee_persistente_severe = diarrhee.diarrhee_presence AND (diarrhee.diarrhee_presence_duree >= 14) AND (@deshydratation_severe)

diarrhee|Pas de déshydratation|low|@pas_de_deshydratation
diarrhee|Signes évidents de déshydratation|medium|@deshydratation_evident
diarrhee|Déshydratation sévère|high|@deshydratation_severe
diarrhee|Diarrhée persistente|medium|@diarrhee_persistente
diarrhee|Diarrhée persistente sévère|high|@diarrhee_persistente_severe
diarrhee|Dysenterie|medium|diarrhee.diarrhee_presence AND diarrhee.sang_selles


@tdr_ok = (fievre.tdr == tdr+) OR (fievre.tdr == non_disponible)
@paludisme_grave = fievre.fievre_presence AND (fievre.raideur_nuque OR fievre.urines_foncees OR fievre.saignements_anormaux OR fievre.ictere OR fievre.choc OR @signes_danger)

@signes_rougeole = (fievre.eruption_cutanee == eruption) AND (fievre.ecoulement_nasal OR fievre.yeux_rouge OR toux.toux_presence)
@rougeole_ou_antecedent = (fievre.eruption_cutanee == antecedent) OR @signes_rougeole
@rougeole_compliquee = fievre.fievre_presence AND @rougeole_ou_antecedent AND (fievre.cornee OR (fievre.ulcerations == profondes_etendues) OR @signes_danger)

fievre|Antécédent de rougeole|low|fievre.fievre_presence AND (fievre.eruption_cutanee == antecedent) AND !fievre.cornee AND !fievre.ecoulement_oculaire AND (fievre.ulcerations == absentes) AND !@signes_danger
fievre|Rougeole|medium|fievre.fievre_presence AND @signes_rougeole AND !fievre.cornee AND !fievre.ecoulement_oculaire AND (fievre.ulcerations == absentes) AND !@signes_danger
fievre|Rougeole avec complication aux yeux ou à la bouche|medium|fievre.fievre_presence AND @rougeole_ou_antecedent AND ((fievre.ecoulement_oculaire OR (fievre.ulcerations == presentes))) AND !@rougeole_compliquee
fievre|Rougeole grave et compliquée|high|@rougeole_compliquee

fievre|Paludisme simple|medium|fievre.fievre_presence AND @tdr_ok AND !@paludisme_grave
fievre|Maladie fébrile très grave ou paludisme grave|high|@paludisme_grave AND @tdr_ok



@mastoidite = oreille.oreille_probleme AND oreille.oreille_gonflement

oreille|Infection chronique de l'oreille|medium|oreille.oreille_probleme AND oreille.oreille_ecoulement AND (oreille.oreille_ecoulement_duree >= 14) AND !@mastoidite
oreille|Infection aiguë de l'oreille|medium|oreille.oreille_probleme AND ((oreille.oreille_ecoulement AND (oreille.oreille_ecoulement_duree < 14)) OR (!oreille.oreille_ecoulement AND oreille.oreille_douleur)) AND !@mastoidite
oreille|Mastoïdite|high|@mastoidite


@anemie_grave = anemie.paleur_palmaire == severe

anemie|Pas d'anémie|low|anemie.paleur_palmaire == absente
anemie|Anémie|medium|anemie.paleur_palmaire == legere
anemie|Anémie grave|high|@anemie_grave



@classifications_graves = @signes_danger OR @pneumonie_grave OR @deshydratation_severe OR @diarrhee_persistente_severe OR @rougeole_compliquee OR @paludisme_grave OR @anemie_grave OR @mastoidite



@ma_normal = (enfant.muac >= 125) AND (enfant.wfh >= 80)
@ma_mam = (enfant.muac >= 115) AND (enfant.wfh >= 70) AND !@ma_normal
@ma_mas = (enfant.muac < 115) OR (enfant.wfh < 70)

malnutrition|Non applicable : enfant de moins de 6 mois|low|enfant.months < 6
malnutrition|Pas de malnutrition|low|(enfant.months >= 6) AND @ma_normal AND (malnutrition.oedemes == no)
malnutrition|MAM|medium|(enfant.months >= 6) AND @ma_mam AND (malnutrition.oedemes == no)
malnutrition|MASs|medium|(enfant.months >= 6) AND @ma_mas AND (malnutrition.test_appetit == positif) AND (malnutrition.oedemes == no) AND !@classifications_graves
malnutrition|MASc|high|(enfant.months >= 6) AND ((malnutrition.oedemes != no) OR (@ma_mas AND ((malnutrition.test_appetit == negatif) OR @classifications_graves)))


@au_moins_deux_maladies = AT_LEAST_TWO_OF(@pneumonie_grave, vih.pneumonie_recidivante, vih.diarrhee_dernierement, oreille.oreille_ecoulement, (enfant.months >= 6) AND !@ma_normal, vih.candidose_buccale, vih.hypertrophie_ganglions_lymphatiques, vih.augmentation_glande_parotide, @diarrhee_persistente OR @diarrhee_persistente_severe)

@au_plus_une_maladie = AT_MOST_ONE_OF(@pneumonie_grave, vih.pneumonie_recidivante, vih.diarrhee_dernierement, oreille.oreille_ecoulement, (enfant.months >= 6) AND !@ma_normal, vih.candidose_buccale, vih.hypertrophie_ganglions_lymphatiques, vih.augmentation_glande_parotide, @diarrhee_persistente)

vih|Pas d'infection à VIH|low|(vih.serologie_enfant == negative) AND (vih.serologie_mere == negative) AND @au_plus_une_maladie
vih|Infection à VIH peu problable|low|(vih.serologie_enfant == inconnue) AND (vih.serologie_mere == inconnue) AND @au_plus_une_maladie
vih|Infection à VIH symptomatique suspecte|medium|(vih.serologie_enfant == inconnue) AND (vih.serologie_mere == inconnue) AND @au_moins_deux_maladies
vih|Infection à VIH possible / exposition au VIH|medium|(((vih.serologie_mere == positive) AND (vih.serologie_enfant == inconnue)) OR ((enfant.months < 18) AND (vih.serologie_enfant == positive))) AND @au_plus_une_maladie
vih|Infection à VIH symptomatique probable|medium|((vih.serologie_enfant == positive) AND (enfant.months < 18)) AND @au_moins_deux_maladies
vih|Infection à VIH confirmée|medium|(((vih.serologie_enfant == positive) AND (enfant.months >= 18)) OR (vih.test_enfant == positif)) AND @au_plus_une_maladie
vih|Infection à VIH symptomatique confirmée|medium|(((vih.serologie_enfant == positive) AND (enfant.months >= 18)) OR (vih.test_enfant == positif)) AND @au_moins_deux_maladies

yield

malnutrition|A risque de malnutrition|low|REMOVED
fievre|Maladie fébrile très grave et TDR négatif|high|@paludisme_grave AND !@tdr_ok
fievre|TDR négatif|medium|fievre.fievre_presence AND !@tdr_ok AND !@paludisme_grave
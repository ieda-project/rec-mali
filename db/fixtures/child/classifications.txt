$version 6

######################
## Signes de danger ##
######################

@signes_danger = danger.boire OR danger.vomit OR danger.convulsions_passe OR danger.lethargie OR danger.convulsions_present

danger|Pas de signe général de danger|low|!@signes_danger
danger|Signe général de danger présent|high|@signes_danger

##########
## Toux ##
##########

@pneumonie_grave = @signes_danger OR toux.stridor
@pneumonie = ((enfant.months >= 12) AND (toux.freq_resp >= 40)) OR ((enfant.months < 12) AND (toux.freq_resp >= 50)) OR toux.tirage

toux|Pas de pneumonie : toux ou rhume|low|toux.toux_presence AND !@pneumonie AND !@pneumonie_grave
toux|Pneumonie|medium|toux.toux_presence AND @pneumonie AND !@pneumonie_grave
toux|Pneumonie grave ou maladie très grave|high|toux.toux_presence AND @pneumonie_grave

##############
## Diarrhée ##
##############

@deshydratation_severe = diarrhee.diarrhee_presence AND AT_LEAST_TWO_OF(diarrhee.conscience_agitation == lethargique_inconscient, diarrhee.yeux_enfonces, diarrhee.soif == impossible, diarrhee.pli_cutane == tres_lentement)

@deshydratation_evident = diarrhee.diarrhee_presence AND AT_LEAST_TWO_OF(diarrhee.conscience_agitation == agite_irritable, diarrhee.yeux_enfonces, diarrhee.soif == avide, diarrhee.pli_cutane == lentement) AND !@deshydratation_severe

@pas_de_deshydratation = diarrhee.diarrhee_presence AND !@deshydratation_evident AND !@deshydratation_severe

@diarrhee_persistente_severe = diarrhee.diarrhee_presence AND (diarrhee.diarrhee_presence_duree >= 14) AND (@deshydratation_severe OR @deshydratation_evident)

@diarrhee_persistente = diarrhee.diarrhee_presence AND (diarrhee.diarrhee_presence_duree >= 14) AND @pas_de_deshydratation

diarrhee|Pas de déshydratation|low|@pas_de_deshydratation
diarrhee|Signes évidents de déshydratation|medium|@deshydratation_evident
diarrhee|Déshydratation sévère|high|@deshydratation_severe
diarrhee|Diarrhée persistente|medium|@diarrhee_persistente
diarrhee|Diarrhée persistente sévère|high|@diarrhee_persistente_severe
diarrhee|Dysenterie|medium|diarrhee.diarrhee_presence AND diarrhee.sang_selles

#######################
## Fièvre / Rougeole ##
#######################

@paludisme_grave = fievre.fievre_presence AND (@signes_danger OR fievre.raideur_nuque OR fievre.fontanelle OR fievre.urines_foncees OR fievre.comportement OR fievre.prostration OR diarrhee.conscience_agitation == lethargique_inconscient OR fievre.choc OR fievre.ictere OR fievre.saignements_spontanes OR fievre.paleur_severe OR toux.tirage OR toux.stridor)
@tdr_positif = fievre.tdr == tdr+

fievre|Maladie fébrile très grave ou paludisme grave|high|@paludisme_grave
fievre|Paludisme simple|medium|fievre.fievre_presence AND @tdr_positif AND !@paludisme_grave
fievre|Fièvre : pas de paludisme|low|fievre.fievre_presence AND !@tdr_positif AND !@paludisme_grave

@rougeole_actuelle = fievre.eruptions AND (fievre.ecoulement_nasal OR fievre.yeux_rouge OR toux.toux_presence)
@rougeole_ou_antecedent = fievre.antecedent_rougeole OR @rougeole_actuelle
@rougeole_compliquee = fievre.fievre_presence AND @rougeole_ou_antecedent AND (@signes_danger OR fievre.cornee OR fievre.ulcerations == profondes_etendues)
@rougeole_yeux_bouche = fievre.fievre_presence AND @rougeole_ou_antecedent AND (fievre.ecoulement_oculaire OR fievre.ulcerations == presentes)

fievre|Rougeole grave et compliquée|high|@rougeole_compliquee
fievre|Rougeole avec complication aux yeux et/ou la bouche|medium|@rougeole_yeux_bouche AND !@rougeole_compliquee
fievre|Rougeole|medium|fievre.fievre_presence AND @rougeole_actuelle AND !@rougeole_yeux_bouche AND !@rougeole_compliquee
fievre|Antécédent de rougeole|low|fievre.fievre_presence AND fievre.antecedent_rougeole AND !@rougeole_actuelle AND !@rougeole_yeux_bouche AND !@rougeole_compliquee

#############
## Oreille ##
#############

@mastoidite = oreille.oreille_gonflement
@infection_aigue = ((oreille.oreille_ecoulement AND oreille.oreille_ecoulement_duree < 14) OR oreille.oreille_douleur)
@infection_chronique = (oreille.oreille_ecoulement AND oreille.oreille_ecoulement_duree >= 14)

oreille|Mastoïdite|high|oreille.oreille_probleme AND @mastoidite
oreille|Infection aiguë de l'oreille|medium|oreille.oreille_probleme AND @infection_aigue AND !@mastoidite
oreille|Infection chronique de l'oreille|medium|oreille.oreille_probleme AND @infection_chronique AND !@infection_aigue AND !@mastoidite
oreille|Pas d'infection d'oreille|low|oreille.oreille_probleme AND !@mastoidite AND !@infection_aigue AND !@infection_chronique

############
## Anémie ##
############

@anemie_grave = anemie.paleur_palmaire == severe OR (anemie.depranocytose AND (fievre.ictere OR enfant.temp >= 38 OR anemie.crises OR anemie.priapisme))

anemie|Anémie grave|high|@anemie_grave
anemie|Anémie|medium|anemie.paleur_palmaire == legere AND !@anemie_grave
anemie|Pas d'anémie|low|anemie.paleur_palmaire == absente AND !@anemie_grave

#################
## Malnutrtion ##
#################

# comparisons with negative values of the zscore do not work in the equations.
# moved the '0' to '100' so that we are sure that we compare with positive values (see application.js)
# -3 => 97
# -2 => 98
# -1 => 99
# 0 => 100
# 2 => 102
# 3 => 103

@complications_medicales = @signes_danger OR (toux.toux_presence AND @pneumonie_grave) OR @deshydratation_severe OR @diarrhee_persistente_severe OR @paludisme_grave OR @rougeole_compliquee OR (oreille.oreille_probleme AND @mastoidite) OR @anemie_grave OR (toux.toux_presence AND @pneumonie AND !@pneumonie_grave AND toux.tirage)

@val_normal = (enfant.wfh_z >= 98) AND (enfant.muac >= 125)
@val_mam = (enfant.wfh_z >= 97) AND (enfant.muac >= 115) AND !@val_normal
@val_mas = (enfant.wfh_z < 97) OR (enfant.muac < 115)

@masc = malnutrition.oedemes OR (@val_mas AND (@complications_medicales OR !malnutrition.atpe OR malnutrition.allaitement))
@mass = @val_mas AND !malnutrition.oedemes AND !@complications_medicales AND malnutrition.atpe AND !malnutrition.allaitement
@mam = @val_mam AND !malnutrition.oedemes
@pas_ma = @val_normal AND !malnutrition.oedemes

malnutrition|Malnutrition aiguë sévère avec complications|high|@masc
malnutrition|Malnutrition aiguë sévère sans complications|medium|@mass
malnutrition|Malnutrition aiguë modérée|medium|@mam
malnutrition|Pas de malnutrition aiguë|low|@pas_ma

#########
## VIH ##
#########

@au_moins_deux_maladies = AT_LEAST_TWO_OF(toux.toux_presence AND @pneumonie AND !@pneumonie_grave, @diarrhee_persistente, @diarrhee_persistente_severe, oreille.oreille_ecoulement, enfant.wfh_z < 97, vih.ganglions, vih.muget, vih.parotide, vih.dermatose)
@au_plus_une_maladie = AT_MOST_ONE_OF(toux.toux_presence AND @pneumonie AND !@pneumonie_grave, @diarrhee_persistente, @diarrhee_persistente_severe, oreille.oreille_ecoulement, enfant.wfh_z < 97, vih.ganglions, vih.muget, vih.parotide, vih.dermatose)

@inf_symp_conf = ((enfant.months >= 18 AND vih.serologie_enfant == positive) OR vih.virologie_enfant == positive) AND @au_moins_deux_maladies

@inf_asymp_conf = ((enfant.months >= 18 AND vih.serologie_enfant == positive) OR vih.virologie_enfant == positive) AND @au_plus_une_maladie

@inf_symp_probable = @au_moins_deux_maladies AND !((enfant.months >= 18 AND vih.serologie_enfant == positive) OR vih.virologie_enfant == positive)

@enfant_expose = vih.vih_mere == positive AND enfant.months < 18 AND @au_plus_une_maladie

@peu_probable = ((vih.vih_mere == inconnu AND vih.virologie_enfant == inconnue AND vih.serologie_enfant == inconnue) OR vih.vih_mere == negatif) AND @au_plus_une_maladie AND !@enfant_expose AND !@inf_asymp_conf AND !((enfant.months < 18 AND vih.virologie_enfant == negative) OR (enfant.months >= 18 AND vih.serologie_enfant == negative))

@pas_vih = ((enfant.months < 18 AND vih.virologie_enfant == negative) OR (enfant.months >= 18 AND vih.serologie_enfant == negative)) AND !@peu_probable AND !@enfant_expose AND !@inf_symp_probable AND !@inf_asymp_conf AND !@inf_symp_conf

vih|Infection a vih symptomatique confirmee|medium|@inf_symp_conf
vih|Infection a vih asymptomatique confirmee|medium|@inf_asymp_conf
vih|Infection a vih symptomatique probable|medium|@inf_symp_probable
vih|Enfant expose/infection a vih possible|medium|@enfant_expose
vih|Infection a vih peu probable|low|@peu_probable
vih|Pas d’infection a vih|low|@pas_vih

yield
